/*
 * Copyright 2014, Mirek Klimos <myreggg@gmail.com>
 */
%{
  #include <iostream>
  #include <cstdlib>
  #include <cctype>
  #include <cstring>
  #include <string>
  #include <vector>

  #include "experiment.h"
  #include "formula.h"
  #include "parser.h"
  #include "game.h"

  #define YYERROR_VERBOSE
  extern "C" int yylex();
  extern "C" int yyparse();
  extern "C" int yylineno;
  extern "C" char* yytext;
  void yyerror (const char *error);

  extern Parser m;
%}

%token T_IDENT
%token T_INT
%token T_STRING

/* order of precedence from
 * http://en.wikipedia.org/wiki/Logical_connective#Order_of_precedence
 */
%right T_EQUIV
%right T_IMPLIES
%left  T_IMPLIED
%left T_OR
%left T_AND
%right T_NOT

%token T_ATLEAST
%token T_ATMOST
%token T_EXACTLY

%token T_VARIABLE
%token T_VARIABLES
%token T_RESTRICTION
%token T_ALPHABET
%token T_MAPPING
%token T_EXPERIMENT
%token T_PARAMS_DISTINCT
%token T_PARAMS_SORTED
%token T_OUTCOME

%union {
  Formula* formula;
  vec<Variable*>* variable_list;
  vec<Formula*>* formula_list;
  vec<string>* string_list;
  vec<uint>* int_list;
  char* tstr;
  uint tint;
}

/* Lets inform Bison about the type of each terminal and non-terminal */
%type <tstr>     T_IDENT
%type <tstr>     T_STRING
%type <tstr>     ident
%type <tint>     T_INT

%type <formula>  formula

%type <string_list> string_list
%type <int_list> int_list
%type <variable_list> variable_list
%type <formula_list> formula_list
%%

start :
    code
  | formula
    {  m.set_only_formula($1);  }

ident :
     T_IDENT
  |  T_STRING

code :
    line
  | code line

line :
    T_VARIABLE ident
    {  m.game().declareVar(m.get<Variable>($2));
       free($2); }
  | T_VARIABLES variable_list
    {  m.game().declareVars($2);  }
  | T_RESTRICTION formula
    {  m.game().addRestriction($2);  }
  | T_ALPHABET string_list
    {  m.game().setAlphabet($2); }
  | T_MAPPING T_STRING variable_list
    {  m.game().addMapping($2, $3);
       free($2); }
  | T_EXPERIMENT T_STRING T_INT
    {  Experiment* e = m.game().addExperiment($2, $3);
       m.set_last_experiment(e);
       free($2); }
  | T_PARAMS_DISTINCT int_list
    { m.last_experiment()->paramsDistinct($2); }
  | T_PARAMS_SORTED int_list
    { m.last_experiment()->paramsSorted($2); }
  | T_OUTCOME T_STRING formula
    {  m.last_experiment()->addOutcome($2, $3);
       free($2); }

variable_list :
    ident
      { $$ = new vec<Variable*>();
        $$->push_back(m.get<Variable>($1));
        free($1); }
  | variable_list ',' ident
      { $$ = $1;
        $$->push_back(m.get<Variable>($3));
        free($3); }

string_list :
    T_STRING
      { $$ = new vec<string>();
        $$->push_back($1);
        free($1); }
  | string_list ',' T_STRING
      { $$ = $1;
        $$->push_back($3);
        free($3); }

int_list :
    T_INT
      { $$ = new vec<uint>();
        $$->push_back($1); }
  | int_list ',' T_INT
      { $$ = $1;
        $$->push_back($3); }

formula :
    '(' formula ')'
      { $$ = $2; }
  | formula T_AND formula
      { $$ = m.OnAssocOp<AndOperator>($1, $3); }
  | formula T_OR formula
      { $$ = m.OnAssocOp<OrOperator>($1, $3); }
  | formula T_IMPLIES formula
      { $$ = m.get<ImpliesOperator>($1, $3); }
  | formula T_IMPLIED formula
      { $$ = m.get<ImpliesOperator>($3, $1); }
  | formula T_EQUIV formula
      { $$ = m.get<EquivalenceOperator>($1, $3); }
  | T_ATLEAST '-' T_INT '(' formula_list ')'
      { $$ = m.get<AtLeastOperator>($3, $5); }
  | T_ATMOST '-' T_INT '(' formula_list ')'
      { $$ = m.get<AtMostOperator>($3, $5); }
  | T_EXACTLY '-' T_INT '(' formula_list ')'
      { $$ = m.get<ExactlyOperator>($3, $5); }
  | T_AND '(' formula_list ')'
      { $$ = m.get<AndOperator>($3); }
  | T_OR '(' formula_list ')'
      { $$ = m.get<OrOperator>($3); }
  | T_NOT formula
      { $$ = m.get<NotOperator>($2); }
  | T_IDENT '$' T_INT
      { m.input_assert($3 > 0 && $3 <= m.last_experiment()->num_params(),
          "Argument $" + std::to_string($3) + " invalid for mapping " + $1);
        $$ = m.get<Mapping>($1, m.game().getMappingId($1), $3);
        free($1); }
  | T_IDENT
      { $$ = m.game().getVarByName($1);
        free($1); }

formula_list:
    formula
      { $$ = new vec<Formula*>();
        $$->push_back($1); }
  | formula_list ',' formula
      { $$ = $1;
        $$->push_back($3); }

%%

void yyerror(const char *error) {
  printf("\n%d: %s at %s\n", yylineno, error, yytext);
  exit(EXIT_FAILURE);
}
